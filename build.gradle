buildscript {
    ext {
        groovyAllVersion = '3.0.8'
        junitVersion = '5.8.1'
        spockVersion = '2.0-groovy-3.0'
        spotlessVersion = '6.0.0'
        spotBugsVersion = '4.8.0'
        checkstyleToolVersion = '9.1'
        pmdToolVersion = '6.40.0'
    }
}

plugins {
    id 'com.diffplug.spotless' version "$spotlessVersion" apply false
    id 'idea'
    id 'com.github.spotbugs' version "$spotBugsVersion" apply false
}

subprojects {

    group 'pl.edu.pw.ljozwiak'
    version '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: 'jacoco'

    dependencies {
        constraints {
            testImplementation "org.codehaus.groovy:groovy-all:$groovyAllVersion"
            testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
            testImplementation "org.spockframework:spock-core:$spockVersion"
            testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
        }
    }

    spotless {
        java {
            googleJavaFormat()
        }
    }

    checkstyle { toolVersion "$checkstyleToolVersion" }

    tasks.withType(Checkstyle) {
        reports {
            xml.required = false
            html.required = true
            html.stylesheet resources.text.fromFile(rootProject.projectDir.getAbsolutePath() + '/config/checkstyle/checkstyle-simple.xsl')
        }
    }

    pmd {
        consoleOutput = true
        toolVersion = "$pmdToolVersion"
    }
}